from AstTree import Node, ASTree, ConnectSame, ConnectWithOps


COMPILER VeKrestKrest

IGNORECASE
CHARACTERS
letter                         = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
digit                          = "0123456789".
cr                             = '\r'.
lf                             = '\n'.
tab                            = '\t'.
strings                        = CHR(32) .. CHR(255) - '"'.
 
TOKENS
identifier                     = letter {letter | digit | "_"}.
number                         = digit {digit} .
string                         = '"' { strings } '"' .

IGNORE cr + lf + tab 

PRODUCTIONS
  VeKrestKrest<out tree>       = (. main_tree = Node(value='VeKrestKrestProg', t='body') .) 
                                 { Defenition<out def_tree> (. main_tree.AddChild(def_tree); #AllInfo(def_tree, "../example.txt", True); print('\n' * 2) .)} 
                                 (. 
                                    tree = ASTree()
                                    tree.AddNode(main_tree)
                                    #tree.PrintTree()
                                 .).

  Defenition<out defTree>      = identifier (. name = self.token.val; name_pos = self.token.pos .) 
                                 Params<out params> "->" Type<out type>
                                 FunctionDefinition<out tree> 
                                 (. 
                                    defTree = Node(value=name, t='FUNCTIONS', start_pos=name_pos - 1, end_pos=name_pos + len(name) - 1) 
                                    tree.Rename('body')
                                    defTree.AddChild(params)
                                    type.Rename('return ' + type.value)
                                    defTree.AddChild(type)
                                    defTree.AddChild(tree)
                                 .) .

  Params<out params>           = (. params = Node(value='params') .)
                                 "(" [ FormalParamList<out fparams> (. params = fparams .) ] ")" .

  FormalParamList<out fparams> = (. fparams = Node(value='params') .) 
                                 FormalParameter<out fp> (. fparams.AddChild(fp) .) 
                                 {"," FormalParameter<out fp> (. fparams.AddChild(fp) .) } .

  FunctionDefinition<out tree> = CompoundStatement<out tree> .
  
  FormalParameter<out fp>      = Type<out type> identifier 
                                 (.
                                    fp = Node(value=self.token.val, t='KEYARG', start_pos=self.token.pos - 1, end_pos=self.token.pos - 1 + len(self.token.val)) 
                                    fp.AddChild(type)
                                 .) .

  Type<out type>               = (. val = ''; end_pos = -2 .)
                                 ( "CELINA" (. val = self.token.val; end_pos = self.token.pos .) [ "["ConstExpression"]" ] 
                                 | "BUKVI"  (. val = self.token.val; end_pos = self.token.pos .)
                                 | "DROB"   (. val = self.token.val; end_pos = self.token.pos .) [ "["ConstExpression"]" ]
                                 | "PRAVDA" (. val = self.token.val; end_pos = self.token.pos .) [ "["ConstExpression"]" ] )
                                 (. type = Node(value='type: ' + val, t='TYPES', start_pos=end_pos - 1, end_pos=end_pos + len(val) - 1) .) .
  
  Statement<out tree>          = ( AssignmentExpression<out assigm_tree> (. tree = assigm_tree .)
                                 | CompoundStatement<out cmpd_tree> (. tree = cmpd_tree .) 
                                 | IfStatement<out if_tree> (. tree = if_tree .)
                                 | ReturnStatement<out ret_tree> (. tree = ret_tree .) 
                                 | WhileStatement<out while_tree> (.  tree = while_tree .) ) .

  DeclrationOrStatement<out t> = ( LocalDeclaration<out declar_tree> (. t = declar_tree .) 
                                 | Statement<out stat_tree> (. t = stat_tree .) ) .

  CompoundStatement<out tree>  = (. tree = Node(t='GRAMMAR_CONSTRUCTION') .)
                                 "{" (. start_pos = self.token.pos .) {DeclrationOrStatement<out t> (. tree.AddChild(t) .)} "}"
                                 (. tree.SetCoords(start_pos - 1, self.token.pos) .) .

  LocalDeclaration<out tree>   = Type<out type> identifier 
                                 (. name = self.token.val; tree = Node(value=name, t='KEYARG', start_pos=self.token.pos - 1, end_pos=self.token.pos - 1 + len(name)) .)
                                 ( "(" [ FormalParamList<out fparams> ] ")" (.  .)
                                 | "=" Expression<out expr_tree> 
                                 (.
                                    tree.AddChild(type)
                                    tree.Rename(name)
                                    value = Node(value='value')
                                    value.AddChild(expr_tree)
                                    tree.AddChild(value)
                                 .) ) ";" .

  ConstExpression              = Expression<out tree> .
  Expression<out tree>         = Conditional<out cond_tree> (. tree = cond_tree .)
                                 [ AssignmentOperator<out op> (. as_pos = self.token.pos .) Expression<out expr_tree> 
                                 (. 
                                    tree = Node(value=op, t='OPERATORS', start_pos=as_pos  - 1, end_pos=as_pos + len(op) - 1)
                                    tree.AddChild(cond_tree)
                                    tree.AddChild(expr_tree) 
                                 .) ] .

  ReturnStatement<out tree>    = "VOZDAT" (. tree = Node(value="VOZDAT", t='KEYWORD', start_pos=self.token.pos - 1, end_pos=self.token.pos + len("VOZDAT") - 1) .) 
                                 [ Expression<out expr_tree> (. tree.AddChild(expr_tree) .) ] ";" .

  WhileStatement<out tree>     = "DOKOLE" (. tree = Node(value='DOKOLE', t='KEYWORD', start_pos=self.token.pos - 1, end_pos=self.token.pos + len("DOKOLE") - 1) .) 
                                 "(" Expression<out expr_tree> ")" Statement<out state_tree> 
                                 (. 
                                    condition = Node("condition")
                                    condition.AddChild(expr_tree)
                                    tree.AddChild(condition)
                                    state_tree.Rename("statements")
                                    tree.AddChild(state_tree)
                                 .) .

  IfStatement<out tree>        = "KOLI" (. tree = Node(value='KOLI', t='KEYWORD', start_pos=self.token.pos - 1, end_pos= self.token.pos + len("KOLI") - 1) .) 
                                 "(" Expression<out expr_tree> ")" Statement<out state_tree> 
                                 (. 
                                    condition = Node("condition")
                                    condition.AddChild(expr_tree)
                                    tree.AddChild(condition)
                                    state_tree.Rename("statements")
                                    tree.AddChild(state_tree)
                                 .)
                                 [ "OTNUD" (. otnud_pos = self.token.pos .) Statement<out else_state_tree> 
                                 (.
                                    else_state_tree.Rename("OTNUD")
                                    else_state_tree.SetCoords(otnud_pos - 1, otnud_pos - len('otnud') - 1)
                                    else_state_tree.type = 'KEYWORD'
                                    tree.AddChild(else_state_tree)
                                 .) ] .
  
  AssignmentExpression<out t>  = Expression<out t> ";" .

  Conditional<out tree>        = LogORExp<out tree>.

  LogORExp<out tree>           = LogANDExp<out and_tree_1> (. tree = and_tree_1 .)
                                 (. trees = [and_tree_1]; ops = []; positions = [] .) 
                                 { "ALI" (. end_pos = self.token.pos .) LogANDExp<out and_tree_2> 
                                 (. 
                                    op = 'ALI'
                                    ops.append(op)
                                    positions.append((end_pos - 1, end_pos + len(op) - 1))
                                    trees.append(and_tree_2) 
                                 .)}  
                                 (. tree = ConnectWithOps(tree, trees, ops, positions) .) .

  LogANDExp<out tree>          = EqualExp<out eq_tree_1> (. tree = eq_tree_1 .)
                                 (. trees = [eq_tree_1]; ops = []; positions = [] .) 
                                 { "DA" (. end_pos = self.token.pos .) EqualExp<out eq_tree_2>
                                 (. 
                                    op = 'DA'
                                    ops.append(op)
                                    positions.append((end_pos - 1, end_pos + len(op) - 1))
                                    trees.append(eq_tree_2) 
                                 .)}  
                                 (. tree = ConnectWithOps(tree, trees, ops, positions) .) .

  EqualExp<out tree>           = RelationExp<out rel_tree_1> (. tree = rel_tree_1 .)
                                 (. trees = [rel_tree_1]; ops = []; positions = [] .) 
                                 { "==" (. end_pos = self.token.pos .) RelationExp<out rel_tree_2> 
                                 (. 
                                    op = '=='
                                    ops.append(op)
                                    positions.append((end_pos - 1, end_pos + len(op) - 1))
                                    trees.append(rel_tree_2) 
                                 .)}  
                                 (. tree = ConnectWithOps(tree, trees, ops, positions) .) .

  RelationExp<out tree>        = AddExp<out sub_tree_1> (. tree = sub_tree_1 .)
                                 (. trees = [sub_tree_1]; ops = []; positions = [] .) 
                                 { ( "<"  (. op  = '<' .)
                                 | ">"  (. op  = '>' .)
                                 | "<=" (. op  = '<=' .)
                                 | ">=" (. op  = '>=' .) ) 
                                 (. end_pos = self.token.pos .)
                                 AddExp<out sub_tree_2> 
                                 (.
                                    ops.append(op)
                                    positions.append((end_pos - len(op), end_pos))
                                    trees.append(sub_tree_2)
                                 .) } 
                                 (. tree = ConnectWithOps(tree, trees, ops, positions) .) .

  AddExp<out tree>             = MultExp<out mult_tree_1> (. tree = mult_tree_1 .)
                                 (. trees = [mult_tree_1]; ops = []; positions = [] .) 
                                 { ( "+" (. op = self.token.val .) 
                                 | "-" (. op = self.token.val .) )
                                 (. end_pos = self.token.pos .) 
                                 MultExp<out mult_tree_2> 
                                 (.
                                    ops.append(op)
                                    positions.append((end_pos - 1, end_pos))
                                    trees.append(mult_tree_2)
                                 .) }
                                 (. tree = ConnectWithOps(tree, trees, ops, positions) .) .

  MultExp<out tree>            = CastExp<out cast_tree_1> (. tree =  cast_tree_1 .) 
                                 (. trees = [cast_tree_1]; ops = []; positions = [] .) 
                                 { ( "*"  (. op = "*" .) 
                                 | "/"  (. op = "/" .)
                                 | "%"  (. op = "%" .) ) 
                                 (. end_pos = self.token.pos .)
                                 CastExp<out cast_tree_2>
                                 (.
                                    ops.append(op)
                                    positions.append((end_pos - 1, end_pos))
                                    trees.append(cast_tree_2)
                                 .) } 
                                 (. tree = ConnectWithOps(tree, trees, ops, positions) .) .

  CastExp<out tree>            = UnaryExp<out unary_tree> (. tree = unary_tree .) .

  UnaryExp<out tree>           = PostFixExp<out s> (. tree = s .) 
                                 | UnaryOperator<out op> (. op_pos = self.token.pos .) CastExp<out cast_tree> 
                                 (.
                                    tree = Node(value=op, t='OPERATORS', start_pos=op_pos - 1, end_pos=op_pos + len(op) - 1)
                                    tree.AddChild(cast_tree) 
                                 .) .

  PostFixExp<out tree>         = Primary<out child> (. tree = child; .)
                                 { "[" Expression<out expression_tree> "]" (. tree.AddChild(expression_tree) .)
                                 | FunctionCall<out f_call_tree>           (. tree = f_call_tree .) }.

  Primary<out prim>            = identifier   (. prim = Node(value=self.token.val, t='KEYARG', start_pos=self.token.pos - 1, end_pos= self.token.pos + len(self.token.val) - 1) .) 
                                 | string     (. prim = Node(value=self.token.val, t='STRING', start_pos=self.token.pos - 1, end_pos= self.token.pos + len(self.token.val) - 1) .) 
                                 | number     (. prim = Node(value=self.token.val, t='CONSTANTS', start_pos=self.token.pos - 1, end_pos= self.token.pos + len(self.token.val) - 1)  .) 
                                 | "(" (. start_pos = self.token.pos .) Expression<out tree> ")" (. end_pos = self.token.pos .)
                                 (. 
                                    prim = Node(value="()", t='BRACKETS', start_pos=start_pos - 1, end_pos=end_pos) 
                                    prim.AddChild(tree) 
                                 .) .

  FunctionCall<out res>        = (. res = Node(value=self.token.val, t='FUNCTIONS', start_pos=self.token.pos - 1, end_pos=self.token.pos + len(self.token.val) - 1) .)
                                 "(" [ ActualParameters<out expr> (. res.AddChild(expr) .) ] ")" .

  ActualParameters<out tree>   = (. tree = Node('params') .) 
                                 Expression<out expr> (. tree.AddChild(expr) .) 
                                 { "," Expression<out expr2> (. tree.AddChild(expr2) .) } .

  AssignmentOperator<out op>   =    "=" (. op = self.token.val .) 
                                 | "*=" (. op = self.token.val .)
                                 | "/=" (. op = self.token.val .)
                                 | "%=" (. op = self.token.val .)
                                 | "+=" (. op = self.token.val .) .

  UnaryOperator<out op>        =   "+"  (. op = self.token.val .) 
                                 | "-"  (. op = self.token.val .) 
                                 | "*"  (. op = self.token.val .) 
                                 | "NE" (. op = self.token.val .) .

END VeKrestKrest.
