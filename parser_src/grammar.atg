from AstTree import Node, ASTree, ConnectSame, ConnectWithOps


COMPILER VeKrestKrest

IGNORECASE
CHARACTERS
letter                         = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
digit                          = "0123456789".
cr                             = '\r'.
lf                             = '\n'.
tab                            = '\t'.
strings                        = CHR(32) .. CHR(255) - '"'.
 
TOKENS
identifier                     = letter {letter | digit | "_"}.
number                         = digit {digit} .
string                         = '"' { strings } '"' .
 
IGNORE cr + lf + tab

PRODUCTIONS
  VeKrestKrest<out tree>       = (. main_tree = Node(value='VeKrestKrestProg', t='body') .) 
                                 { Defenition<out def_tree> (. main_tree.AddChild(def_tree) .)} 
                                 (. 
                                    tree = ASTree()
                                    tree.AddNode(main_tree)
                                 .).

  Defenition<out defTree>      = (. start_pos = self.token.pos .) 
                                 identifier (. name = self.token.val; end_pos = self.token.pos .) 
                                 Params<out params> "->" Type<out type>
                                 FunctionDefinition<out tree> 
                                 (. 
                                    defTree = Node(value=f'{name}', t='FUNCTIONS', start_pos=start_pos, end_pos=end_pos) 
                                    tree.Rename('body')
                                    defTree.AddChild(params)
                                    type.Rename('return ' + type.value)
                                    defTree.AddChild(type)
                                    defTree.AddChild(tree)
                                 .) .

  Params<out params>           = (. params = Node(value='params') .)
                                 "(" [ FormalParamList<out fparams> (. params = fparams .) ] ")" .

  FormalParamList<out fparams> = (. fparams = Node(value='params') .) 
                                 FormalParameter<out fp> (. fparams.AddChild(fp) .) 
                                 {"," FormalParameter<out fp> (. fparams.AddChild(fp) .) } .

  FunctionDefinition<out tree> = CompoundStatement<out tree> .
  
  FormalParameter<out fp>      = Type<out type> (. start_pos = self.token.pos .) 
                                 identifier (. fp = Node(value=self.token.val, t='KEYARG', start_pos=start_pos, end_pos=self.token.pos) .) .

  Type<out type>               = (. start_pos = self.token.pos .)
                                 ( "CELINA" (. val=self.token.val .) [ "["ConstExpression"]" ] 
                                 | "BUKVI"  (. val=self.token.val .)
                                 | "DROB"   (. val=self.token.val .) [ "["ConstExpression"]" ]
                                 | "PRAVDA" (. val=self.token.val .) [ "["ConstExpression"]" ] )
                                 (. type = Node(value='type: ' + val, t='TYPES', start_pos=start_pos, end_pos=self.token.pos) .) .
  
  Statement<out tree>          = ( AssignmentExpression<out assigm_tree> (. tree = assigm_tree .)
                                 | CompoundStatement<out cmpd_tree> (. tree = cmpd_tree .) 
                                 | IfStatement<out if_tree> (. tree = if_tree .)
                                 | ReturnStatement<out ret_tree> (. tree = ret_tree .) 
                                 | WhileStatement<out while_tree> (.  tree = while_tree .) ) .

  DeclrationOrStatement<out t> = ( LocalDeclaration<out declar_tree> (. t = declar_tree .) 
                                 | Statement<out stat_tree> (. t = stat_tree .) ) .

  CompoundStatement<out tree>  = (. tree = Node(t='body') .)
                                 "{"(.start_pos = self.token.pos.) 
                                 {DeclrationOrStatement<out t> (. tree.AddChild(t) .)} 
                                 (. end_pos = self.token.pos .) "}" 
                                 (. tree.SetCoords(start_pos, end_pos) .) .

  LocalDeclaration<out tree>   = Type<out type> (. start_pos = self.token.pos .) identifier 
                                 (. name = self.token.val ; tree = Node(value=name, t='KEYARG', start_pos=start_pos, end_pos=self) .)
                                 ( "(" [ FormalParamList<out fparams> ] ")" (.  .)
                                 | "=" Expression<out expr_tree> 
                                 (.
                                    tree.AddChild(type)
                                    tree.Rename(name)
                                    value = Node(value='value')
                                    value.AddChild(expr_tree)
                                    tree.AddChild(value)
                                 .) ) ";" .

  ConstExpression              = Expression<out tree> .
  Expression<out tree>         = Conditional<out cond_tree> (. tree = cond_tree .)
                                 { AssignmentOperator<out op> Expression<out expr_tree> 
                                 (. 
                                    tree = Node(t='expr')
                                    tree.AddChild(cond_tree)
                                    tree.Rename(op)
                                    tree.AddChild(expr_tree) 
                                 .) } .

  ReturnStatement<out tree>    = (. start_pos = self.token.pos .)
                                 "VOZDAT" (. tree = Node(value="VOZDAT", t='KEYWORD', start_pos=start_pos, end_pos=self.token.pos) .) 
                                 [ Expression<out expr_tree> (. tree.AddChild(expr_tree) .) ] ";" .

  WhileStatement<out tree>     = (. start_pos = self.token.pos .)
                                 "DOKOLE" (. tree = Node(value='DOKOLE', t='KEYWORD', start_pos=start_pos, end_pos=self.token.pos) .) 
                                 "(" Expression<out expr_tree> ")" Statement<out state_tree> 
                                 (. 
                                    condition = Node("condition")
                                    condition.AddChild(expr_tree)
                                    tree.AddChild(condition)
                                    state_tree.Rename("statements")
                                    tree.AddChild(state_tree)
                                 .) .

  IfStatement<out tree>        = (. start_pos = self.token.pos .)
                                 "KOLI" (. tree = Node(value='KOLI', t='KEYWORD', start_pos=start_pos, end_pos=self.token.pos) .) 
                                 "(" Expression<out expr_tree> ")" Statement<out state_tree> 
                                 (. 
                                    condition = Node("condition")
                                    condition.AddChild(expr_tree)
                                    tree.AddChild(condition)
                                    state_tree.Rename("statements")
                                    tree.AddChild(state_tree)
                                 .)
                                 [ "OTNUD" Statement<out else_state_tree> 
                                 (.
                                    else_state_tree.Rename("OTNUD")
                                    tree.AddChild(else_state_tree)
                                 .) ] .
  
  AssignmentExpression<out t>  = Expression<out t> ";" .

  Conditional<out tree>        = LogORExp<out tree>.

  LogORExp<out tree>           = LogANDExp<out and_tree_1> (. tree = and_tree_1 .)
                                 (. trees = [and_tree_1] .)
                                 { "ALI" LogANDExp<out and_tree_2> (. trees.append(and_tree_2) .)}
                                 (. tree = ConnectSame(tree, trees, 'ALI') .) .

  LogANDExp<out tree>          = EqualExp<out eq_tree_1> (. tree = eq_tree_1 .)
                                 (. trees = [eq_tree_1] .)
                                 { "DA" EqualExp<out eq_tree_2> (. trees.append(eq_tree_2) .)} 
                                 (. tree = ConnectSame(tree, trees, 'DA') .) .

  EqualExp<out tree>           = RelationExp<out rel_tree_1> (. tree = (rel_tree_1) .)
                                 (. trees = [rel_tree_1] .)
                                 { ( "==" ) RelationExp<out rel_tree_2> (. trees.append(rel_tree_2) .)}  
                                 (. tree = ConnectSame(tree, trees, '==') .) .

  RelationExp<out tree>        = (. start_pos = self.token.pos .)
                                 AddExp<out sub_tree_1> (. tree = sub_tree_1 .)
                                 (. trees = [sub_tree_1]; ops = [] .) 
                                 { ( "<"  (. op  = '<' .)
                                 | ">"  (. op  = '>' .)
                                 | "<=" (. op  = '<=' .)
                                 | ">=" (. op  = '>=' .) ) 
                                 AddExp<out sub_tree_2> 
                                 (.
                                    ops.append(op)
                                    trees.append(sub_tree_2)
                                 .) } 
                                 (. tree = ConnectWithOps(tree, trees, ops, start_pos, self.token.pos) .) .

  AddExp<out tree>             = (. start_pos = self.token.pos .)
                                 MultExp<out mult_tree_1> (. tree = mult_tree_1 .)
                                 (. trees = [mult_tree_1]; ops = [] .) 
                                 { ( "+" (. op = self.token.val .) 
                                 | "-" (. op = self.token.val .) ) 
                                 MultExp<out mult_tree_2> 
                                 (.
                                    ops.append(op)
                                    trees.append(mult_tree_2)
                                 .) }
                                 (. tree = ConnectWithOps(tree, trees, ops, start_pos, self.token.pos) .) .

  MultExp<out tree>            = (. start_pos = self.token.pos .)
                                 CastExp<out cast_tree_1> (. tree =  cast_tree_1 .) 
                                 (. trees = [cast_tree_1]; ops = [] .)   
                                 { ( "*"  (. op = "*" .) 
                                 | "/"  (. op = "/" .)
                                 | "%"  (. op = "%" .) ) 
                                 CastExp<out cast_tree_2>
                                 (.
                                    ops.append(op)
                                    trees.append(cast_tree_2)
                                 .) } 
                                 (. tree = ConnectWithOps(tree, trees, ops, start_pos, self.token.pos) .) .

  CastExp<out tree>            = UnaryExp<out urary_tree> (. tree = urary_tree .) .

  UnaryExp<out tree>           = PostFixExp<out s> (. tree = s .) | UnaryOperator<out op> CastExp<out cast_tree>
                                 (.
                                    tree = Node()
                                    tree.Rename(op)
                                    tree.AddChild(cast_tree) 
                                 .) .

  PostFixExp<out tree>         = Primary<out child> (. tree = child; .)
                                 { "[" Expression<out expression_tree> "]" (. tree.AddChild(expression_tree) .)
                                 | FunctionCall<out f_call_tree>           (. tree = f_call_tree .) }.

  Primary<out prim>            = identifier   (. prim = Node(value=self.token.val, t='IDENTIFIER', start_pos=(self.token.pos - len(self.token.val)), end_pos=self.token.pos) .) 
                                 | string     (. prim = Node(value=self.token.val, t='STRING', start_pos=(self.token.pos - len(self.token.val)), end_pos=self.token.pos) .) 
                                 | number     (. prim = Node(value=self.token.val, t='CONSTANTS', start_pos=(self.token.pos - len(self.token.val)), end_pos=self.token.pos) .) 
                                 | "(" (. start_pos = self.token.pos .) Expression<out tree> ")" (. end_pos = self.token.pos .)
                                 (. 
                                    prim = Node(value="()", t='BRACKETS', start_pos=start_pos, end_pos=end_pos) 
                                    prim.AddChild(tree) 
                                 .) .

  FunctionCall<out res>        = (. res = Node(value=self.token.val, t='FUNCTIONS') .)
                                 "(" [ ActualParameters<out expr> (. res.AddChild(expr) .) ] ")" .

  ActualParameters<out tree>   = (. tree = Node('params') .) 
                                 Expression<out expr> (. tree.AddChild(expr) .) 
                                 { "," Expression<out expr2> (. tree.AddChild(expr2) .) } .

  AssignmentOperator<out op>   =    "=" (. op = self.token.val .) 
                                 | "*=" (. op = self.token.val .)
                                 | "/=" (. op = self.token.val .)
                                 | "%=" (. op = self.token.val .)
                                 | "+=" (. op = self.token.val .) .

  UnaryOperator<out op>        =   "+"  (. op = self.token.val .) 
                                 | "-"  (. op = self.token.val .) 
                                 | "*"  (. op = self.token.val .) 
                                 | "NE" (. op = self.token.val .) .

END VeKrestKrest.
